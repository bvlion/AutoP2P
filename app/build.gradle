plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.github.triplet.play' version '3.6.0'
}

android {
    compileSdk 31
    apply from: '../dependencies/signing.gradle', to: android
    def globalConfiguration = rootProject.extensions.getByName("ext")

    defaultConfig {
        applicationId "net.ambitious.android.autop2p"
        minSdk 21
        targetSdk 31
        versionCode globalConfiguration["appVersionCode"]
        versionName globalConfiguration["appVersionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        manifestPlaceholders = [
            admob_key: globalConfiguration["adMobCode"]
        ]
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
            buildConfigField "String", "AD_MOB_KEY", "\"" + globalConfiguration["adMobKey"] + "\""
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            buildConfigField "String", "AD_MOB_KEY", "\"" + globalConfiguration["adMobKey"] + "\""
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.0.4'
        kotlinCompilerVersion '1.5.31'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    // compose
    def compose_version = captureVersion(implementation('androidx.compose.ui:ui:1.0.4'))
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

import com.github.triplet.gradle.androidpublisher.ReleaseStatus

play {
    track.set("production")
    serviceAccountCredentials.set(file("./publisher-google-services.json"))
    defaultToAppBundles.set(true)
    // full release
    releaseStatus.set(ReleaseStatus.COMPLETED)
    // staged release
//    userFraction.set(0.3)
//    releaseStatus.set(ReleaseStatus.IN_PROGRESS)
}